find_path(OpenJpeg_INCLUDE_DIR openjpeg.h PATH_SUFFIXES openjpeg)

find_library(OpenJpeg_LIBRARY_RELEASE NAMES openjpeg)
find_library(OpenJpeg_LIBRARY_DEBUG NAMES openjpegd)

include(SelectLibraryConfigurations)
select_library_configurations(OpenJpeg) #sets OpenJpeg_LIBRARY using OpenJpeg_LIBRARY_DEBUG and OpenJpeg_LIBRARY_RELEASE

set(OpenJpeg_INCLUDE_DIRS ${OpenJpeg_INCLUDE_DIR})
mark_as_advanced(OpenJpeg_INCLUDE_DIR)

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(OpenJpeg REQUIRED_VARS OpenJpeg_INCLUDE_DIR OpenJpeg_LIBRARY)
set(OpenJpeg_FOUND ${OPENJPEG_FOUND})

if(${OpenJpeg_FOUND})
  # Opticks' default OpenJpeg-2.0.0 does not have a VERSION macro in it's headers, so try pkg-config
  # This should all work. pkg-config is standard on CentOS, and all modern openjpeg2-2.x.y support it.
  # But at this time Opticks does not REQUIRE PkgConfig, so we make no assumption.
  set(OPENJP2_FOUND FALSE)
  find_package(PkgConfig)
  if(${PKG_CONFIG_FOUND})
    set(ENV{PKG_CONFIG_PATH}  "${OpenJpeg_LIBRARY_DIR}/pkgconfig:${PKG_CONFIG_PATH}")
    pkg_search_module (OPENJP2 libopenjp2)
    if(OPENJP2_FOUND)
      string(STRIP ${OPENJP2_VERSION} OPENJP2_VERSION)
      set(OpenJpeg_VERSION ${OPENJP2_VERSION})
      add_compile_options(-DOpenJpeg_VERSION=${OpenJpeg_VERSION})
      message("Found OpenJpeg_VERSION: " ${OpenJpeg_VERSION})
      # we'd really like OPJ_VERSION_MAJOR and MINOR, if we can parse them
      string(FIND ${OPENJP2_VERSION} "." DOTPOS)
      # strip off MINOR and PATCH numbers
      string(SUBSTRING ${OPENJP2_VERSION} 0 ${DOTPOS}   OPJ_VERSION_MAJOR)
      MATH(EXPR DOTPOS "${DOTPOS}+1")
      # store remainder MINOR.PATCH numbers in OPJ_VERSION_MINOR_BUILD
      string(SUBSTRING ${OPENJP2_VERSION} ${DOTPOS} -1   OPJ_VERSION_MINOR_BUILD)
      # then strip off trailing BUILD number to get VERSION_MINOR
      string(FIND ${OPJ_VERSION_MINOR_BUILD} "." DOTPOS)
      string(SUBSTRING ${OPJ_VERSION_MINOR_BUILD} 0 ${DOTPOS}   OPJ_VERSION_MINOR)
      # and finally the BUILD number
      MATH(EXPR DOTPOS "${DOTPOS}+1")
      string(SUBSTRING ${OPJ_VERSION_MINOR_BUILD} ${DOTPOS} -1   OPJ_VERSION_BUILD)
      
      message("OPJ_VERSION_MAJOR: " ${OPJ_VERSION_MAJOR})
      message("OPJ_VERSION_MINOR: " ${OPJ_VERSION_MINOR})
      message("OPJ_VERSION_BUILD: " ${OPJ_VERSION_BUILD})
    endif()
  endif()
  if(NOT OPENJP2_FOUND)
    message("pkgconfig/libopenjp2 not found, cannot determine OpenJpeg version. Using 2.0.0")
    set(OpenJpeg_VERSION "2.0.0")
    set(OPJ_VERSION_MAJOR 2)
    set(OPJ_VERSION_MINOR 0)
    set(OPJ_VERSION_BUILD 0)
  endif()
  MATH(EXPR OPJ_VERSION_NUMBER "100*100*${OPJ_VERSION_MAJOR} + 100*${OPJ_VERSION_MINOR} + ${OPJ_VERSION_BUILD}")
  add_compile_options(-DOPJ_VERSION_NUMBER=${OPJ_VERSION_NUMBER})
endif()
